group 'mayer.pub'
version '0.1.1'

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
	}
}

apply plugin: 'java'
apply plugin: 'antlr'
apply plugin: 'idea'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
	jcenter()
}

dependencies {
	antlr 'org.antlr:antlr4:4.5.3'

	testCompile "org.antlr:antlr4-runtime:4.5.3"
	testCompile 'junit:junit:4.12'
}

generateGrammarSource {
	maxHeapSize = "64m"
	arguments += ['-package', 'pub.mayer.arithmetic']
	outputDirectory = file("src/generated/java/pub/mayer/arithmetic")
}

compileJava {
	dependsOn generateGrammarSource
	source = ['src/generated/java', 'src/main/java']
}

clean {
	delete file("src/generated")
}

idea {
	module {
		sourceDirs += file("src/generated/java")
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_KEY')
	filesSpec {
		from 'build/libs'
		into '.'
	}
	pkg {
		repo = 'betazoid'
		name = project.name
		publicDownloadNumbers = true
		version {
			name = project.version
		}
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.4'
}

